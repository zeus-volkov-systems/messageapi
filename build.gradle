plugins {
    id 'groovy'
    id 'maven-publish'
    id 'java'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
}

shadowJar {
    baseName = 'messageapi'
    classifier = 'all'
    version = "${messageApiVersion}"
    manifest {
        attributes 'Implementation-Title': 'messageapi-all',
                   'Implementation-Version': "${messageApiVersion}"
    }
}


publishing {
  publications {
    maven(MavenPublication) {
       groupId 'gov.noaa'
       artifactId 'messageapi'
       version "${messageApiVersion}"
       from components.java
    }
    shadow(MavenPublication) { publication ->
       groupId 'gov.noaa'
       artifactId 'messageapi'
       version "${messageApiVersion}"
       project.shadow.component(publication)
    }
  }
  repositories {
    mavenLocal()
  }
}


sourceCompatibility = "${javaVersion}"
targetCompatibility = "${javaVersion}"

defaultTasks 'clean', 'rebuild', 'uploadShadow', 'publishToMavenLocal', 'groovydoc'


configurations {
    // configuration that holds jars to include in the jar
    directDeps
}

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    directDeps ("com.googlecode.json-simple:json-simple:${jsonSimpleVersion}")
    compile ("commons-net:commons-net:${apacheCommonsNetVersion}",
             "org.codehaus.groovy:groovy-all:${groovyVersion}")
    testCompile ("org.slf4j:slf4j-simple:${slf4jVersion}",
                "org.spockframework:spock-core:${spockVersion}")
    testRuntime ("com.athaydes:spock-reports:${spockReportsVersion}")
    configurations.compile.extendsFrom(configurations.directDeps)
}

jar {
    manifest {
        attributes 'Implementation-Title': 'MessageAPI',
                   'Implementation-Version': "${messageApiVersion}"
    }
    /*from {
        configurations.directDeps.collect { 
            it.isDirectory() && it.getPath().contains("json-simple") ? it : project.logger.lifecycle(it.getPath()) 
        }
    }*/
}

sourceSets {
    main {
        java.srcDirs = ['src/java/main', 'src/java/test']
        groovy.srcDirs = ['src/java/main']
        resources.srcDirs = ['resources/main']
    }
    test {
        java.srcDirs = ['src/java/test','src/java/main']
        groovy.srcDirs = ['src/groovy/test']
        resources.srcDirs = ['resources/test']
    }
}

test {
    systemProperty 'org.slf4j.simpleLogger.log.com.athaydes.spockframework', 'warn'
    minHeapSize = "512m"
    maxHeapSize = "2048m"
    testLogging {
        showStandardStreams = true
    }
}


task rebuild(dependsOn: [build, test]) {
    doLast {
        println "MessageAPI Version ${messageApiVersion} built successfully."
    }
}
